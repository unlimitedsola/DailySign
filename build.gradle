plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.2.30"
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

group 'love.sola.spigot.dailysign'
version '1.0'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
}

ext.spigot_version = '1.12.2-R0.1-SNAPSHOT'

dependencies {
    compileOnly "org.spigotmc:spigot-api:$spigot_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

def serverDir = 'D:\\Game\\MinecraftTestServers\\1.12.2'

task copyToTestServer(type: Copy) {
    from "build/libs/$project.name-$version-all.jar"
    into "$serverDir/plugins/"
}

tasks.copyToTestServer.dependsOn 'shadowJar'
tasks.copyToTestServer.group = 'mcdebug'

task debug(type: Exec) {
    workingDir serverDir
    commandLine 'cmd', '/c', 'start.bat'
    standardInput = System.in
}

tasks.debug.dependsOn 'copyToTestServer'
tasks.debug.group = 'mcdebug'

task removeTestServerConfig(type: Delete) {
    delete("$serverDir/plugins/$project.name/")
}

tasks.removeTestServerConfig.group = 'mcdebug'
